apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "de.tum.in.net.interceptls"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 8
        versionName "1.0.6"
    }

    //sign build with the corresponding key from tls-interception-secret repository if exists
    def keystoreDir = rootProject.file("../../tls-interception-secret/tls-client-android/")
    def keystorePropertiesFile = new File(keystoreDir, "keystore.properties")
    if (keystorePropertiesFile.exists()) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        signingConfigs {
            release {
                v1SigningEnabled true
                v2SigningEnabled true
                storeFile new File(keystoreDir, keystoreProperties['storeFile'])
                storePassword  keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        if (keystorePropertiesFile.exists()) {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation group: 'org.slf4j', name: 'slf4j-android', version: '1.7.25'
    implementation 'de.tum.in.net:tls-capture:1.0.6'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    //implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'

}
